file: tags game

tags: [_WS] (tag [_WS])*

tag: "[" tag_key _WS tag_value "]"

?tag_key: CNAME

?tag_value: ESCAPED_STRING

game: [_WS] (turn _WS)* [turn]

turn: INT "." _WS ply [_WS ply]

ply: (drop | move | climb) [[from_location] to_location] [win] [[_WS] comment]

move: piece

drop: "+" piece

climb: piece "x" piece

from_location: location "-"

to_location: location

location: piece [neighbour_reference]

neighbour_reference: piece ["(" neighbour_reference ")"] [angle]

piece: "Q"   -> white_queen
      | "q"  -> black_queen
      | "G"  -> white_grasshopper
      | "g"  -> black_grasshopper
      | "A"  -> white_ant
      | "a"  -> black_ant
      | "S"  -> white_spider
      | "s"  -> black_spider
      | "B"  -> white_beetle
      | "b"  -> black_beetle
      | "M"  -> white_mosquito
      | "m"  -> black_mosquito

angle: "1".."5"

win: "#"

comment: "{" [/[^}]+/] "}" | ";" [/[^\n]+/] "\n"

%import common.CNAME
%import common.ESCAPED_STRING
%import common.INT
%import common.WS  -> _WS
